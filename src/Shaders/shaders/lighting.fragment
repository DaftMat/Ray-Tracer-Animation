#version 330 core
out vec4 FragColor;

#define NR_TEXTURE_MAX 256

struct Material {
    sampler2D texture_diffuse1;
    sampler2D texture_specular1;
    vec3 defaultDiffuse;
    vec3 defaultSpecular;
    bool diffTex;
    bool specTex;
    float shininess;
    bool png;
};

struct DirLight {
    vec3 direction;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct PointLight {
    vec3 position;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct SpotLight {
    vec3 position;
    vec3 direction;
    float cutOff;
    float outerCutOff;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

#define NR_POINT_LIGHTS 1

in vec3 FragPos;
in vec3 Normal;
in vec2 TexCoords;

uniform vec3 viewPos;
uniform DirLight dirLight;
uniform PointLight pointLights[NR_POINT_LIGHTS];
uniform SpotLight spotLight;
uniform Material material;

// function prototypes
vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir);
vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir);
vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir);

float linearizeDepth(float depth, float far);

float near = 0.1;
float farFog = 1.0;
float farCam = 100.0;

void main()
{
    if (material.png && material.diffTex) {
        vec4 texColor = texture(material.texture_diffuse1, TexCoords);
        if (texColor.a < 0.1)
            discard;
    }
    // properties
    vec3 norm = normalize(Normal);
    vec3 viewDir = normalize(viewPos - FragPos);

    // == =====================================================
    // Our lighting is set up in 3 phases: directional, point lights and an optional flashlight
    // For each phase, a calculate function is defined that calculates the corresponding color
    // per lamp. In the main() function we take all the calculated colors and sum them up for
    // this fragment's final color.
    // == =====================================================
    // phase 1: directional lighting
    //vec3 result = CalcDirLight(dirLight, norm, viewDir);
    vec3 result = vec3(0.0);
    // phase 2: point lights
    for(int i = 0; i < NR_POINT_LIGHTS; i++)
        result += CalcPointLight(pointLights[i], norm, FragPos, viewDir);
    // phase 3: spot light
    //result += CalcSpotLight(spotLight, norm, FragPos, viewDir);
    float fog = linearizeDepth(gl_FragCoord.z, farFog);
    //result = (1.0-fog) * result + fog * vec3(0.6);

    FragColor = vec4(result, 1.0);
}

// calculates the color when using a directional light.
vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir)
{
    vec3 lightDir = normalize(-light.direction);
    vec3 halfwayDir = normalize(lightDir + viewDir);
    // diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);
    // specular shading
    float spec = pow(max(dot(normal, halfwayDir), 0.0), material.shininess);
    // combine results
    vec3 ambient = light.ambient * (material.diffTex ? vec3(texture(material.texture_diffuse1, TexCoords)) : material.defaultDiffuse);
    vec3 diffuse = light.diffuse * diff * (material.diffTex ? vec3(texture(material.texture_diffuse1, TexCoords)) : material.defaultDiffuse);
    vec3 specular = light.specular * spec * (material.specTex ? vec3(texture(material.texture_specular1, TexCoords)) : material.defaultSpecular);
    return (ambient + diffuse + specular);
}

// calculates the color when using a point light.
vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
{
    vec3 lightDir = normalize(light.position - fragPos);
    vec3 halfwayDir = normalize(lightDir + viewDir);
    // diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);
    // specular shading
    float spec = pow(max(dot(normal, halfwayDir), 0.0), material.shininess);
    // attenuation
    float distance = length(light.position - fragPos);
    float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));
    // combine results
    vec3 ambient = light.ambient * (material.diffTex ? vec3(texture(material.texture_diffuse1, TexCoords)) : material.defaultDiffuse);
    vec3 diffuse = light.diffuse * diff * (material.diffTex ? vec3(texture(material.texture_diffuse1, TexCoords)) : material.defaultDiffuse);
    vec3 specular = light.specular * spec * (material.specTex ? vec3(texture(material.texture_specular1, TexCoords)) : material.defaultSpecular);
    ambient *= attenuation;
    diffuse *= attenuation;
    specular *= attenuation;
    return (ambient + diffuse + specular);
}

// calculates the color when using a spot light.
vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
{
    vec3 lightDir = normalize(light.position - fragPos);
    vec3 halfwayDir = normalize(lightDir + viewDir);
    // diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);
    // specular shading
    float spec = pow(max(dot(normal, halfwayDir), 0.0), material.shininess);
    // attenuation
    float distance = length(light.position - fragPos);
    float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));
    // spotlight intensity
    float theta = dot(lightDir, normalize(-light.direction));
    float epsilon = light.cutOff - light.outerCutOff;
    float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);
    // combine results
    vec3 ambient = light.ambient * (material.diffTex ? vec3(texture(material.texture_diffuse1, TexCoords)) : material.defaultDiffuse);
    vec3 diffuse = light.diffuse * diff * (material.diffTex ? vec3(texture(material.texture_diffuse1, TexCoords)) : material.defaultDiffuse);
    vec3 specular = light.specular * spec * (material.specTex ? vec3(texture(material.texture_specular1, TexCoords)) : material.defaultSpecular);
    ambient *= attenuation * intensity;
    diffuse *= attenuation * intensity;
    specular *= attenuation * intensity;
    return (ambient + diffuse + specular);
}

float linearizeDepth(float depth, float far) {
    float z = depth * 2.0 - 1.0;
    return ((2.0 * near * far) / (far + near - z * (far - near))) / far;
}